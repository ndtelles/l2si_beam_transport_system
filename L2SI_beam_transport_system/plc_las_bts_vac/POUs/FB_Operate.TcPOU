<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.17">
  <POU Name="FB_Operate" Id="{8ee47251-2ac9-09b7-2785-97e47b267431}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Operate
VAR_INPUT
	i_bCancel : BOOL; // Flag to cancel the current operation
END_VAR
VAR_IN_OUT
	iq_eOperation : E_Op; // Select which operation to run, if any. Outputs the currently running operation
	iq_eTransportTube : T_TransportTube; // Select which transport tube to run certain operations on
	iq_sMessageBuffer : STRING(EPICS_STRING_SIZE); // Buffer for current step in human readable form
END_VAR
VAR_OUTPUT
	q_bBusy : BOOL := FALSE; // Flag for if an operation is running
	q_bError : BOOL := FALSE; // Whether there was an error with the last run operation
END_VAR
VAR
	eCurrOp : E_Op; // The currently running operation
	eCurrTransportTube : T_TransportTube; // The current transport tube being operated on (if operation involves transport tube)
	refRecentOp : REFERENCE TO FB_Operation; // The function block of the most recent run operation
	
	(* Operations *)
	fbPumpTransportTube : FB_PumpTransportTube;
	fbVentTransportTube : FB_VentTransportTube;
	fbOpenTransportTube : Fb_OpenTransportTube;
	fbPumpSwitchbox : FB_PumpSwitchbox;
	fbSlowPumpSwitchbox : FB_SlowPumpSwitchbox;
	fbVentPumpStation : FB_VentPumpStation;
	fbVentSwitchbox : FB_VentSwitchbox;
	fbSlowVentSwitchbox : FB_SlowVentSwitchbox;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Run operations, but don't start any new operations if another operation is still running *)

// Only start new operations if the system is NOT busy with another operation!
IF NOT q_bBusy THEN
	eCurrOp := iq_eOperation;
	eCurrTransportTube := iq_eTransportTube;
END_IF

// Execute selected operation
CASE eCurrOp OF
E_Op.PTT: // Pump transport tube
	refRecentOp REF= fbPumpTransportTube;
	fbPumpTransportTube(
		i_bExecute := TRUE, 
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer,
		i_eTransportTube := eCurrTransportTube
	);

E_Op.VTT: // Vent transport tube
	refRecentOp REF= fbVentTransportTube;
	fbVentTransportTube(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer,
		i_eTransportTube := eCurrTransportTube
	);
	
E_Op.OTT: // Open transport tube gate valve
	refRecentOp REF= fbOpenTransportTube;
	fbOpenTransportTube(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer,
		i_eTransportTube := eCurrTransportTube
	);
	
E_Op.PSB: // Pump switchbox
	refRecentOp REF= fbPumpSwitchbox;
	fbPumpSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer
	);
	
E_Op.SPSB: // Slow pump switchbox
	refRecentOp REF= fbSlowPumpSwitchbox;
	fbSlowPumpSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer
	);
	
E_Op.VPS: // Vent pump station
	refRecentOp REF= fbVentPumpStation;
	fbVentPumpStation(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer,
	);

E_Op.VSB: // Vent switchbox
	refRecentOp REF= fbVentSwitchbox;
	fbVentSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer,
	);
	
E_Op.SVSB: // Slow vent switchbox
	refRecentOp REF= fbSlowVentSwitchbox;
	fbSlowVentSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		iq_sMessageBuffer := iq_sMessageBuffer
	);
END_CASE

IF __ISVALIDREF(refRecentOp) THEN
	// Check values from the most recently run operation	
	q_bBusy := refRecentOp.q_bBusy;
	q_bError := refRecentOp.q_bError;
ELSE
	// No operation has been run yet. Stick with default values
	q_bBusy := FALSE;
	q_bError := FALSE;
END_IF

// If all operation have finished then set operation to none
IF NOT q_bBusy THEN
	eCurrOp := E_Op.NONE;
END_IF

// Set output operation to whatever is ACTAULLY currently running
iq_eOperation := eCurrOp;
iq_eTransportTube := eCurrTransportTube;]]></ST>
    </Implementation>
    <LineIds Name="FB_Operate">
      <LineId Id="1551" Count="5" />
      <LineId Id="1558" Count="29" />
      <LineId Id="1597" Count="57" />
      <LineId Id="801" Count="0" />
      <LineId Id="1655" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>