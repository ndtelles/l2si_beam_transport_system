<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.17">
  <POU Name="FB_Operate" Id="{8ee47251-2ac9-09b7-2785-97e47b267431}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Operate
VAR_INPUT
	i_bCancel : BOOL; // Flag to cancel the current operation
END_VAR
VAR_IN_OUT
	iq_eOperation : E_Op; // Select which operation to run, if any. Outputs the currently running operation
	iq_eTransportTube : E_TransportTube; // Select which transport tube to run certain operations on
	iq_sMessageBuff : STRING(EPICS_STRING_SIZE); // Buffer for current step in human readable form
END_VAR
VAR_OUTPUT
	q_bBusy : BOOL := FALSE; // Flag for if an operation is running
END_VAR
VAR
	eCurrOp : E_Op; // The currently running operation
	eCurrTransportTube : E_TransportTube; // The current transport tube being operated on (if operation involves transport tube)
	refRunningOp : REFERENCE TO FB_Operation;
	
	(* Operations *)
	fbPumpTransportTube : FB_PumpTransportTube;
	fbVentTransportTube : FB_VentTransportTube;
	fbOpenTransportTube : Fb_OpenTransportTube;
	fbPumpSwitchbox : FB_PumpSwitchbox;
	fbSlowPumpSwitchbox : FB_SlowPumpSwitchbox;
	fbVentPumpStation : FB_VentPumpStation;
	fbVentSwitchbox : FB_VentSwitchbox;
	fbSlowVentSwitchbox : FB_SlowVentSwitchbox;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Run operations, but don't start any new operations if another operation is still running *)

// Only start new operations if the system is NOT busy with another operation!
IF NOT q_bBusy THEN
	eCurrOp := iq_eOperation;
	eCurrTransportTube := iq_eTransportTube;
END_IF

// Execute selected operation
CASE eCurrOp OF
E_Op.PTT: // Pump transport tube
	refRunningOp REF= fbPumpTransportTube;
	fbPumpTransportTube(
		i_bExecute := TRUE, 
		i_bCancel := i_bCancel,
		i_eTransportTube := eCurrTransportTube
	);

E_Op.VTT: // Vent transport tube
	refRunningOp REF= fbVentTransportTube;
	fbVentTransportTube(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		i_eTransportTube := eCurrTransportTube
	);
	
E_Op.OTT: // Open transport tube gate valve
	refRunningOp REF= fbOpenTransportTube;
	fbOpenTransportTube(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel,
		i_eTransportTube := eCurrTransportTube
	);
	
E_Op.PSB: // Pump switchbox
	refRunningOp REF= fbPumpSwitchbox;
	fbPumpSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel
	);
	
E_Op.SPSB: // Slow pump switchbox
	refRunningOp REF= fbSlowPumpSwitchbox;
	fbSlowPumpSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel
	);
	
E_Op.VPS: // Vent pump station
	refRunningOp REF= fbVentPumpStation;
	fbVentPumpStation(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel
	);

E_Op.VSB: // Vent switchbox
	refRunningOp REF= fbVentSwitchbox;
	fbVentSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel
	);
	
E_Op.SVSB: // Slow vent switchbox
	refRunningOp REF= fbSlowVentSwitchbox;
	fbSlowVentSwitchbox(
		i_bExecute := TRUE,
		i_bCancel := i_bCancel
	);
END_CASE

IF __ISVALIDREF(refRunningOp) THEN
	STRNCPY(pDst := ADR(iq_sMessageBuff), pSrc := ADR(refRunningOp.q_sMessage), nDstSize := SIZEOF(iq_sMessageBuff));
	q_bBusy := refRunningOp.q_bBusy;
	
	// Allow current operation to call other operations. Don't allow call if the current operation is still busy
	IF refRunningOp.q_eCallOp <> E_Op.NONE AND NOT refRunningOp.q_bBusy THEN
		eCurrOp := refRunningOp.q_eCallOp;
		// Make sure busy signal stays on if we are going to be calling another operation
		q_bBusy := TRUE;
	END_IF 
ELSE
	iq_sMessageBuff := 'IDLE';
	q_bBusy := FALSE;
END_IF

// If all operation have finished then set operation to none
IF NOT q_bBusy THEN
	eCurrOp := E_Op.NONE;
END_IF

// Set output operation to whatever is currently running
iq_eOperation := eCurrOp;
iq_eTransportTube := eCurrTransportTube;]]></ST>
    </Implementation>
    <LineIds Name="FB_Operate">
      <LineId Id="518" Count="91" />
      <LineId Id="404" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>